#  Copyright (C) Cfengine AS

#  This file is part of Cfengine 3 - written and maintained by Cfengine AS.

#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU General Public License as published by the
#  Free Software Foundation; version 3.

#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA

# To the extent this program is licensed as part of the Enterprise
# versions of Cfengine, the applicable Commerical Open Source License
# (COSL) may apply to this file if you as a licensee so wish it. See
# included file COSL.txt.

#######################################################
#
# The starting point for every configuration
#
#######################################################

body common control

{
inputs  => { 
           "update.cf", 
           "library.cf"
           };

bundlesequence  => { 
                   update("policy_host.domain.tld"), # set localhost to ignore
                   "main",
                   "cfengine2"
                   };

output_prefix => "cfengine>";
version => "1.2.3";
}

#######################################################

body runagent control
{
hosts => { "127.0.0.1:5308", "eternity.iu.hio.no:80", "slogans.iu.hio.no" };
#output_to_file => "true";
}

#########################################################

body server control 

{
allowconnects         => { "127.0.0.1" , "::1" };
allowallconnects      => { "127.0.0.1" , "::1" };
trustkeysfrom         => { "127.0.0.1" , "::1" };
cfruncommand          => "/home/mark/.cfagent/bin/cf-agent";
allowusers            => { "mark", "root" };
}

#########################################################

bundle server access_rules()

{
access:

  "/home/mark/LapTop"

    admit   => { "127.0.0.1" };

  "/home/mark/.cfagent/bin/cf-agent"

    admit   => { "127.0.0.1" };

roles:

  ".*"  authorize => { "mark" };
}

#######################################################

bundle agent main

{
vars:

  "sys_files"    slist        => { 
                                 "/etc/passwd", 
                                 "/etc/services" 
                                 };
files:

  "$(sys_files)" perms        => p("root","0644"),
                 changes      => trip_wire;

  "/etc/shadow"  perms        => p("root","0600"),
                 changes      => trip_wire;

  "/usr"         changes      => trip_wire,
                 depth_search => recurse("inf");

  "/tmp"         delete       => tidy,
                 file_select  => days("2"),
                 depth_search => recurse("inf");

}

#######################################################

bundle agent cfengine2

{
commands:

  "$(sys.workdir)/bin/cfagent";
}