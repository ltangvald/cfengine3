#######################################################
#
# Test that backslashes are correctly passed to commands (Issue 471)
#
#######################################################

body common control
{
  inputs => { "../../default.cf" };
  bundlesequence  => { "g", default("$(this.promise_filename)") };   
  version => "1.0";
nova_edition::
  host_licenses_paid => "5";
}

bundle common g
{
vars:
  "testfile" string => "/tmp/TEST.cfengine";
}

#######################################################

bundle agent init
{
files:
  "$(g.testfile)"
    create => "true",
    edit_defaults => empty,
    edit_line => setup_lines;
}

body edit_defaults empty
{
empty_file_before_editing => "true";
}

bundle edit_line setup_lines
{
insert_lines:
    "a test";
    "a.test";
    "nottest";
}

#######################################################

bundle agent test
{
vars:
  "all_lines" string => execresult("/bin/egrep -c '.' $(g.testfile)", "useshell");
  "re_lines" string => execresult("/bin/egrep -c 'a.test' $(g.testfile)", "useshell");
  "lit_lines" string => execresult("/bin/egrep -c 'a\.test' $(g.testfile)", "useshell");
  "doubleslash_lit_lines" string => execresult("/bin/egrep -c 'a\\.test' $(g.testfile)", "useshell");
}

#######################################################

bundle agent check
{
classes:
    "ok" and => {
                  strcmp("$(test.all_lines)", "3"),
                  strcmp("$(test.re_lines)", "2"),
                  strcmp("$(test.lit_lines)", "1"),
                  strcmp("$(test.doubleslash_lit_lines)", "1")
                };

reports:
  DEBUG::
    "all_lines: $(test.all_lines) =?= 3";
    "re_lines: $(test.re_lines) =?= 2";
    "lit_lines: $(test.lit_lines) =?= 1";
    "doubleslash_lit_lines: $(test.doubleslash_lit_lines) =?= 1";
  ok::
    "$(this.promise_filename) Pass";
  !ok::
    "$(this.promise_filename) FAIL";
}

#######################################################

bundle agent fini
{
vars:
	"dummy" string => "dummy";
}
