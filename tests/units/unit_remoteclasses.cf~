########################################################
#
# Remote classes from server connection to cfServer
#
########################################################

 #
 # run this as follows:
 #
 # cf-serverd -f runtest_1.cf [-d2]
 # cf-agent   -f runtest_2.cf
 #
 # Notice that the same file configures all parts of cfengine

########################################################

body common control

{
bundlesequence  => { "testbundle"  };

version => "1.2.3";
}

########################################################

bundle agent testbundle

{
classes:

 "succeeded" expression => remoteclassesmatching("job_fin.*","127.0.0.1","yes","myprefix");


 # client_host::


reports:

  succeeded::

    "Succeeded";

  myprefix_job_finished::

    "Remote job finished - time to do our bit";

  !succeeded::

    "Failed to get classes";



 # server_host::

commands:

   "/bin/echo Job finished"

     classes => signal_repaired("job_finished");

}

#########################################################

body classes signal_repaired(x)
{
promise_repaired => { "$(x)" };
persist_time => "10";
}

#########################################################
# Server config
#########################################################

body server control 

{
allowconnects         => { "127.0.0.1" , "::1" };
allowallconnects      => { "127.0.0.1" , "::1" };
trustkeysfrom         => { "127.0.0.1" , "::1" };
allowusers            => { "mark" };
}

#########################################################

bundle server access_rules()

{
vars:

  "localvar" string => "literal string";

access:

  "job_finish.*"

        handle  => "test_class_signal",
  resource_type => "context",
        admit   => { "127.0.0.1" };
}
