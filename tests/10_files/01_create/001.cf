#######################################################
#
# Create a file, check defaults
#
#######################################################

body common control
{
  inputs => { "../../default.cf" };
  bundlesequence  => { "g", default("$(this.promise_filename)") };   
  version => "1.0";
nova_edition::
  host_licenses_paid => "5";
}

bundle common g
{
vars:
	"testfile" string => "/tmp/TEST.cfengine";

	# This extracts the octal mode, and decimal nlink, uid, gid, size
	"command" string => 'printf "%o" . " %d" x 4, (stat("$(g.testfile)"))[2]&07777, (stat(_))[3..5,7]';
}

#######################################################

bundle agent init
{
files:
	"$(g.testfile)"
	    delete => init_delete;
}

body delete init_delete
{
	dirlinks => "delete";
	rmdirs   => "true";
}

#######################################################

bundle agent test
{

files:
	"$(g.testfile)"
	    create => "true";
}

#######################################################

bundle agent check
{
vars:
	# We want default 0600, 1 link, zero size
	"expect" string => "600 1 \d+ \d+ 0";

	"result" string => execresult(
	    "/usr/bin/perl -le '$(g.command)'",
	    "noshell");

classes:
	"ok" expression => regcmp("$(expect)", "$(result)");

reports:
    DEBUG::
	"expected: '$(expect)'";
	"got:      '$(result)'";
    ok::
	"$(this.promise_filename) Pass";
    MAIN.ok::
	"$(this.promise_filename) Pass"
		report_to_file => "$(G.logfile)";
    !ok::
	"$(this.promise_filename) FAIL";
    MAIN.!ok::
	"$(this.promise_filename) FAIL"
		report_to_file => "$(G.logfile)";
}

#######################################################

bundle agent fini
{
methods:
	"any" usebundle => default_fini("$(g.testfile)");
}
