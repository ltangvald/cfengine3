#######################################################
#
# Test that timestamped copying adds .cfsaved suffix (Issue 666)
#
#######################################################

body common control
{
  inputs => { "../../default.cf" };
  bundlesequence  => { "g", default("$(this.promise_filename)") };   
  version => "1.0";
nova_edition::
  host_licenses_paid => "5";
}

bundle common g
{
vars:
	"testdir" string => "/tmp/TEST.cfengine";
}

#######################################################

bundle agent init
{
files:
  "$(g.testdir)/afile"
    create => "true";

# We can't use files promise here as it will remember we have created the file
# and will not generate backup file which we are trying to get.
commands:
  "/bin/echo LimpBizkit > $(g.testdir)/destfile"
    contain => shell;
}

body contain shell
{
  useshell => "true";
}

#######################################################

bundle agent test
{
files:
    "$(g.testdir)/destfile"
      copy_from => cp_2_file("$(g.testdir)/afile");
}

body copy_from cp_2_file(x) {
  source => "$(x)";
  copy_backup => "timestamp";
  compare => "digest";
}

#######################################################

bundle agent check
{
files:
  "$(g.testdir)/destfile.*\.cfsaved"
    touch => "true",
    classes => ifrepaired("ok");

reports:
    ok::
	"$(this.promise_filename) Pass";
    !ok::
	"$(this.promise_filename) FAIL";
}

body classes ifrepaired(classname)
{
promise_repaired => { "$(classname)" };
}

#######################################################

bundle agent fini
{
methods:
	"any" usebundle => default_fini("$(g.testdir)");
}
